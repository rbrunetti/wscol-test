grammar org.xtext.example.xpt.Xpt with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate xpt "http://www.xtext.org/example/xpt/Xpt"

Model:
	declarations+=Declaration*
	assertionSet=AssertionOr ';';

Declaration:
	'let' var=Variable '=' assert=(Assertion | AssertionQuantifiedBoolean) ';';

AssertionOr returns Assertions:
	AssertionAnd ({AssertionOr.left=current} '||' right=AssertionAnd)*;

AssertionAnd returns Assertions:
	HighProrityAssertion ({AssertionAnd.left=current} '&&' right=HighProrityAssertion)*;

HighProrityAssertion returns Assertions:
	AssertionNot | AssertionBraced | AssertionForm;

AssertionBraced:
	'(' innerFormula=AssertionOr ')';

AssertionNot:
	'!' '(' innerFormula=AssertionOr ')';

AssertionForm:
	leftAssert=Assertion op=Rop rightAssert=Assertion | leftAssert=AssertionQuantifiedBoolean;

Assertion:
	query=Query ('.' function=Function)? | AssertionQuantifiedNumeric | constant=Constant | boolean=BOOLEAN | '[' values=Values ']';

AssertionQuantifiedBoolean returns AssertionQuantified:
	quantifier=BoolQuantifier '(' alias=Variable 'in' var=Variable ',' conditions=AssertionOr ')';

AssertionQuantifiedNumeric returns AssertionQuantified:
	quantifier=NumbQuantifier '(' alias=Variable 'in' var=Variable ',' conditions=AssertionOr ')';

Query:
	'(' steps+=Step+ ')' | steps+=Step+;

Step:
	'/' name=ID ('[' attribute=Attribute ']')? | placeholder=Variable;

Attribute:
	property=ID (op=Rop numberValue=NUMBER | op=StringRop strValue=STRING) | number=NUMBER;

StringRop:
	'==' | "!=";

Rop:
	StringRop | '<' | '<=' | '>' | '>=';

Function:
	name=ID '(' (params=Values)? ')';

BoolQuantifier:
	'forall' | 'exists';

NumbQuantifier:
	'numOf' | 'sum' | 'avg' | 'min' | 'max' | 'product';

Variable:
	'$' (ID | BoolQuantifier | NumbQuantifier);

Values:
	value+=Value (',' value+=Value)*;

Value:
	Constant | var=Variable;

Constant:
	number=NUMBER | string=STRING;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';

terminal NUMBER returns ecore::EDouble:
	('-')? ('0'..'9')* ('.' ('0'..'9')+)?;

terminal INT returns ecore::EInt: // override della regola di default
	'this one has been deactivated';